<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
    creationComplete="creationComplete(event)"
    show="showHandler(event)"
    width="100%"
    height="100%">
    
    <mx:Metadata>
        [ResourceBundle("OrderPage")]
    </mx:Metadata>

    <mx:Script>
        <![CDATA[
            import com.j_nid.models.Product;
            import mx.events.ItemClickEvent;
        	import com.j_nid.controls.ApplicationManager;
        	import mx.controls.Alert;
        	import mx.events.CloseEvent;
            import mx.resources.ResourceBundle;
            import mx.collections.SortField;
            import mx.collections.Sort;
            import mx.collections.ArrayCollection;
            import com.j_nid.utils.Utils;
            import com.j_nid.models.Person;
            import mx.binding.utils.ChangeWatcher;
            import com.j_nid.events.JNidEvent;
            import com.j_nid.utils.CairngormUtils;
            import mx.events.FlexEvent;
            import mx.collections.ListCollectionView;
            import mx.events.ListEvent;
            import mx.events.CalendarLayoutChangeEvent;
            import com.j_nid.utils.PrintUtils;
            import com.j_nid.models.Order;
            import com.j_nid.models.OrderItem;
            
            [Bindable]
            private var people:ArrayCollection;
            [Bindable]
            private var orders:ArrayCollection;
            [Bindable]
            private var selectedOrder:Order;
            [Bindable]
            private var appMgr:ApplicationManager =
                ApplicationManager.getInstance();
            private var formatPrice:Function = Utils.formatPrice;
            private var productSelector:ProductSelector;
            
            private function creationComplete(e:FlexEvent):void {
                // Create people.
                people = new ArrayCollection(Person.all());
                var sort:Sort = new Sort();
                sort.fields = [new SortField("name")];
                people.sort = sort;
                people.filterFunction =
                    function(obj:Object):Boolean {
                        return Person(obj).isCustomer;
                    };
                personField.dataProvider = people;
                // Create orders.
                orders = new ArrayCollection(Order.all());
                sort = new Sort();
                sort.fields = [new SortField("created", false, true)];
                orders.sort = sort;
                orders.filterFunction =
                    function(obj:Object):Boolean {
                        var order:Order = Order(obj);
                        return checkPerson(order.person) &&
                               checkStatus(order) &&
                               checkDate(order.created);
                    };
                orderList.dataProvider = orders;
                // Add change watchers.
                ChangeWatcher.watch(personField, "selectedIndex", filterOrder);
                ChangeWatcher.watch(startDateField, "selectedDate", filterOrder);
                ChangeWatcher.watch(endDateField, "selectedDate", filterOrder);
                ChangeWatcher.watch(outstandingField, "selected", filterOrder);
                ChangeWatcher.watch(paidField, "selected", filterOrder);
                currentState = "TodayOrder";
            }
            
            private function orderDeleted(e:JNidEvent):void {
                orders.refresh();
                selectedOrder = null;
            }
            
            private function orderItemDeleted(e:JNidEvent):void {
                // set null first for invoke data binding.
                selectedOrder = null;
                selectedOrder = e.data.order;
            }
            
            private function showHandler(e:FlexEvent):void {
                people.refresh();
                if (orderTabBar.selectedIndex == 0) {
                    initTodayOrder();
                } else if (orderTabBar.selectedIndex == 1) {
                    
                } else if (orderTabBar.selectedIndex == 2) {
                    initOrderByDate();
                }
            }
            
            private function initOrderByDate():void {
                initOrderByCustomer();
                personField.selectedIndex = -1;
            }
            
            private function initOrderByCustomer():void {
                outstandingField.selected = true;
                var now:Date = new Date();
                var rangeEnd:Date = new Date(now.fullYear, now.month, now.date);
                var rangeStart:Date = Utils.moveDateByDay(rangeEnd, -7);
                startDateField.selectedDate = rangeStart;
                endDateField.selectedDate = rangeEnd;
                startDateField.enabled = true;
                endDateField.enabled = true;
            }
            
            private function initTodayOrder():void {
                outstandingField.selected = true;
                paidField.selected = true;
                var now:Date = new Date();
                var today:Date = new Date(now.fullYear, now.month, now.date);
                startDateField.selectedDate = today;
                endDateField.selectedDate = today;
                startDateField.enabled = false;
                endDateField.enabled = false;
                personField.selectedIndex = -1;
            }
            
            private function filterOrder(e:Event):void {
                orders.refresh();
            }
            
            private function checkPerson(person:Person):Boolean {
                if (personField.selectedItem == null) {
                    return true;
                }
                return person == personField.selectedItem;
            }
            
            private function checkDate(date:Date):Boolean {
                var start:Date = startDateField.selectedDate;
                var end:Date = endDateField.selectedDate;
                if (start != null && end != null) {
                    // Move end date to the midnight of the next day.
                    end = Utils.moveDateByDay(end, 1);
                    if (date >= start && date < end) {
                        return true;
                    }
                }
                return false;
            }
            
            private function checkStatus(order:Order):Boolean {
                var statusArray:Array = new Array();
                if (outstandingField.selected && order.isOutstanding) {
                    return true;
                }
                if (paidField.selected && order.isPaid) {
                    return true;
                }
                return false;
            }
            
            private function printOrder(e:MouseEvent):void {
                var order:Order = Order(orderList.selectedItem);
                PrintUtils.printOrder(order);
            }
            
            private function deleteOrderClick(e:MouseEvent):void {
                var order:Order = Order(orderList.selectedItem);
                appMgr.showConfirm(
                    resourceManager.getString('SupplyPage',
                        'ConfirmMessage'),
                    resourceManager.getString('SupplyPage',
                        'ConfirmTitle'),
                    function(e:CloseEvent):void {
                        if (e.detail == Alert.OK) {
                            order.addEventListener(JNidEvent.ORDER_DELETED,
                                orderDeleted);
                            order.remove();
                        }
                    });
            }
            
            private function searchChange(e:Event):void {
                var sort:Sort = new Sort();
                sort.fields = [new SortField("sortIndex"), new SortField("name")];
                people.sort = sort;
                people.filterFunction = 
                    function(obj:Object):Boolean {
                        var person:Person = Person(obj);
                        return person.isCustomer &&
                               person.name.indexOf(personSearchField.text) != -1;
                    };
                people.refresh();
            }
            
            private function itemListKeyDown(e:KeyboardEvent):void {
                var item:OrderItem = OrderItem(orderItemList.selectedItem);
                if (e.keyCode == Keyboard.DELETE && item != null) {
                    appMgr.showConfirm(
                        resourceManager.getString('OrderPage',
                            'ConfirmMessage'),
                        resourceManager.getString('OrderPage',
                            'ConfirmTitle'),
                        function(e:CloseEvent):void {
                            if (e.detail == Alert.OK) {
			                    if (item.order.orderItems.length > 1) {
			                        item.remove();
			                    } else {
			                        item.addEventListener(
                                        JNidEvent.ORDER_ITEM_DELETED,
                                        orderItemDeleted);
			                        item.order.remove();
			                    }
			                }
                        });
                }
            }
            
            private function tabBarClick(e:ItemClickEvent):void {
                if (orderTabBar.selectedIndex == 0) {
                    currentState = "TodayOrder";
                    initTodayOrder();
                } else if (orderTabBar.selectedIndex == 1) {
                    currentState = "";
                    initOrderByDate();
                } else if (orderTabBar.selectedIndex == 2) {
                    currentState = "TodayOrder";
                    initOrderByDate();
                }
            }
            
            private function formatPriceGrid(item:Object,
                                             column:DataGridColumn):String {
                                              
                return formatPrice(item[column.dataField]);
            }
            
            private function addOrderItemClick(e:MouseEvent):void {
                var productSelector:ProductSelector =
                        ProductSelector(appMgr.createPopUp(ProductSelector));
                productSelector.addEventListener(JNidEvent.SELECTED_PRODUCT,
                    addOrderItem);
            }
            
            private function addOrderItem(e:JNidEvent):void {
                var product:Product = Product(e.data);
                var orderItem:OrderItem = new OrderItem();
                orderItem.productID = product.id;
                orderItem.unit = product.unit;
                orderItem.pricePerUnit = product.pricePerUnit;
                orderItem.costPerUnit = product.costPerUnit;
                for each (var obj:OrderItem in orderItemList.dataProvider) {
                    if (orderItem.productID == obj.productID) {
                        obj.unit += orderItem.unit;
                        obj.save();
                        return;
                    }
                }
                orderItem.orderID = Order(orderList.selectedItem).id;
                orderItem.save();
                orderItemList.dataProvider.addItem(orderItem);
                orderItemList.verticalScrollPosition = 
                    orderItemList.maxVerticalScrollPosition;
            }
        ]]>
    </mx:Script>
    
    <mx:states>
        <mx:State name="TodayOrder">
            <mx:RemoveChild target="{searchBox}"/>
            <mx:RemoveChild target="{personField}"/>
        </mx:State>
    </mx:states>
    
    <mx:TabBar id="orderTabBar"
        itemClick="tabBarClick(event)">
        
        <mx:dataProvider>
            <mx:Array>
                <mx:String>
                    {resourceManager.getString('OrderPage', 'TodayOrder')}
                </mx:String>
                <mx:String>
                    {resourceManager.getString('OrderPage',
                        'OrderByCustomer')}
                </mx:String>
                <mx:String>
                    {resourceManager.getString('OrderPage',
                        'OrderByDate')}
                </mx:String>
            </mx:Array>
        </mx:dataProvider>
    </mx:TabBar>
    
    <mx:HBox
        width="100%"
        height="100%">
        
        <mx:Panel id="customerPnl"
            title="{resourceManager.getString('OrderPage', 'Filter')}"
            verticalGap="5"
            paddingTop="5"
            paddingBottom="5"
            width="220"
            height="100%">
            
            <mx:HBox id="searchBox"
                paddingLeft="5"
                paddingRight="5"
                verticalAlign="middle"
                width="100%">
                
                <mx:Label
                    text="{resourceManager.getString('OrderPage',
                                'SearchCustomer')}:"/>
            
                <mx:TextInput id="personSearchField"
                    change="searchChange(event)"
                    width="100%"/>
            </mx:HBox>
            
            <mx:List id="personField"
                labelField="name"
                borderThickness="0"
                width="100%"
                height="100%"/>
                
            <mx:VBox id="dateRangeBox"
                horizontalAlign="left"
                width="100%">
                
                <mx:HBox
                    horizontalAlign="right"
                    width="180">
                    
                    <mx:Label text="{resourceManager.getString('OrderPage',
                                'StartDate')}:"/>
                    
                    <mx:DateField id="startDateField"/>
                </mx:HBox>
                
                <mx:HBox
                    horizontalAlign="right"
                    width="180">
                    
                    <mx:Label text="{resourceManager.getString('OrderPage',
                                'EndDate')}:"/>
                    
                    <mx:DateField id="endDateField"/>
                </mx:HBox>
            </mx:VBox>
                
            <mx:VBox
                paddingLeft="5"
                paddingBottom="5"
                paddingRight="5"
                width="100%">
                
                <mx:CheckBox id="outstandingField"
                    label="{resourceManager.getString('OrderPage', 'Outstanding')}"/>
                
                <mx:CheckBox id="paidField"
                    label="{resourceManager.getString('OrderPage', 'Paid')}"/>
            </mx:VBox>
        </mx:Panel>
        
        <mx:Panel title="{resourceManager.getString('OrderPage', 'Order')}" 
            layout="absolute"
            width="250"
            height="100%">
            
            <mx:List id="orderList"
                change="selectedOrder=Order(orderList.selectedItem)"
                width="100%"
                height="100%">
                
                <mx:itemRenderer>
                    <mx:Component>
    <mx:Label text="{data}" color="{data.isPaid ? 0x0000FF:0xFF0000}"/>
                    </mx:Component>
                </mx:itemRenderer>
            </mx:List>
        </mx:Panel>
        
        <mx:Panel title="{resourceManager.getString('OrderPage', 'Detail')}" 
            layout="vertical"
            paddingLeft="5"
            paddingRight="5" 
            paddingTop="5"
            width="100%"
            height="100%">
            
            <mx:HBox 
                horizontalAlign="right"
                paddingTop="5"
                paddingRight="5" 
                width="100%"
                height="35">
                
                <mx:Label id="customerNameField"
                    text="{selectedOrder.person.name}"/>
                
                <mx:Label id="createdDateField"
                    text="{Utils.formatDate(selectedOrder.created,
                                'DD/MM/YY J:NN:SS')}"/>
            </mx:HBox>
            
            <mx:DataGrid id="orderItemList" 
                dataProvider="{selectedOrder.orderItems}"
                textAlign="center"
                keyDown="itemListKeyDown(event)"
                headerStyleName="centerHeader"
                editable="true"
                width="100%"
                height="100%">
                
                <mx:columns>
                    <mx:DataGridColumn
                        headerText="{resourceManager.getString(
                                           'OrderPage', 'Quantity')}"
                        dataField="quantity"
                        editorDataField="value"
                        width="50">
                        
                        <mx:itemEditor>
                            <mx:Component>
<mx:NumericStepper minimum="0" maximum="1000" 
    change="{data.quantity = value;data.save();}"/>
                            </mx:Component>
                        </mx:itemEditor>
                    </mx:DataGridColumn>
                    
                    <mx:DataGridColumn
                        headerText="{resourceManager.getString('OrderPage', 'Name')}"
                        dataField="name"
                        editable="false"/>
                    
                    <mx:DataGridColumn
                        headerText="{resourceManager.getString('OrderPage', 'Unit')}"
                        dataField="unit"
                        textAlign="right"
                        editorDataField="value"
                        width="50">
                        
                        <mx:itemEditor>
                            <mx:Component>
<mx:NumericStepper minimum="1" maximum="100000" 
    change="{data.unit = value;data.save();}"/>
                            </mx:Component>
                        </mx:itemEditor>
                    </mx:DataGridColumn>
                    
                    <mx:DataGridColumn
                        headerText="{resourceManager.getString(
                                           'OrderPage', 'PricePerUnit')}"
                        dataField="pricePerUnit"
                        labelFunction="formatPriceGrid"
                        textAlign="right"
                        editorDataField="value"
                        width="85">
                        
                        <mx:itemEditor>
                            <mx:Component>
<mx:NumericStepper minimum="1" maximum="10000" stepSize="0.25" 
    change="{data.pricePerUnit = value;data.save();}"/>
                            </mx:Component>
                        </mx:itemEditor>
                    </mx:DataGridColumn>
                    
                    <mx:DataGridColumn
                        headerText="{resourceManager.getString('OrderPage', 'Total')}"
                        dataField="total"
                        labelFunction="formatPriceGrid"
                        textAlign="right"
                        editable="false"
                        width="85"/>
                </mx:columns>
            </mx:DataGrid>
            
            <mx:HBox
                horizontalAlign="right"
                paddingRight="5"
                width="100%"
                height="35">
                
                <mx:Label
                    id="summaryField"
                    text="{resourceManager.getString('OrderPage', 'Summary',
                                [formatPrice(selectedOrder.total),
                                 formatPrice(selectedOrder.paidTotal)])}"/>
            </mx:HBox>
            
            <mx:ControlBar
            	horizontalAlign="right">
            	
            	<mx:Button
                    label="{resourceManager.getString('OrderPage', 'Add')}"
                    enabled="{orderList.selectedItem != null}"
                    click="addOrderItemClick(event)"/>
            
                <mx:Button
                	label="{resourceManager.getString('OrderPage', 'Delete')}"
                	enabled="{orderList.selectedItem != null}"
                    click="deleteOrderClick(event)"/>
                
                <mx:Button
                	label="{resourceManager.getString('OrderPage', 'Print')}"
                    enabled="{orderList.selectedItem != null}"
                    click="printOrder(event)"/>
            </mx:ControlBar>
        </mx:Panel>
    </mx:HBox>
</mx:VBox>
