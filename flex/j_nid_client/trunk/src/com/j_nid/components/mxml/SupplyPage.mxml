<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="createtionComplete(event)"
	show="showHandler(event)"
	width="100%" height="100%">
	
	<mx:Metadata>
		[ResourceBundle("SupplyPage")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import mx.resources.ResourceBundle;
			import com.j_nid.utils.Utils;
			import mx.collections.ListCollectionView;
			import mx.binding.utils.ChangeWatcher;
			import mx.events.FlexEvent;
			import com.j_nid.models.Supply;
			import com.j_nid.models.Person;
			import mx.collections.SortField;
			import mx.collections.Sort;
			import mx.collections.ArrayCollection;
			import com.j_nid.utils.CairngormUtils;
			import com.j_nid.events.JNidEvent;
			import com.j_nid.models.SupplyItem;
			import com.j_nid.models.JNidModelLocator;
			
			[Bindable]
			private var model:JNidModelLocator = JNidModelLocator.getInstance();
			[Bindable]
            private var people:ArrayCollection;
            [Bindable]
            private var supplies:ArrayCollection;
            private var utils:Utils = Utils.getInstance();
			
			private function createtionComplete(e:FlexEvent):void {
				// Create people.
				people = new ArrayCollection(model.people);
				var sort:Sort = new Sort();
                sort.fields = [new SortField("name")];
                people.sort = sort;
                people.filterFunction = 
                    function(obj:Object):Boolean {
                        return Person(obj).isSupplier;
                    };
                supplierList.dataProvider = people;
                // Create supplies.
                supplies = new ArrayCollection(model.supplies);
                sort = new Sort();
                sort.fields = [new SortField("created", false, true)];
                supplies.sort = sort;
                supplies.filterFunction =
                    function(obj:Object):Boolean {
                        var supply:Supply = Supply(obj);
                        return checkSupplier(supply.person) &&
                               checkDate(supply.created);
                    };
                supplyList.dataProvider = supplies; 
                // Add change watchers.
				ChangeWatcher.watch(supplierList, "selectedIndex", filterSupply);
				ChangeWatcher.watch(dateField, "selectedRanges", filterSupply);
			}
			
			private function showHandler(e:FlexEvent):void {
				people.refresh();
                initData();
            }
			
			private function initData():void {
				var today:Date = new Date();
				var rangeEnd:Date = new Date(today.fullYear, today.month, today.date);
				var rangeStart:Date = utils.moveDateByDay(rangeEnd, -7);
				dateField.selectedRanges = [{rangeStart: rangeStart, rangeEnd: rangeEnd}];
			}
			
			private function filterSupply(e:Event):void {
				supplies.refresh();
			}
			
			private function checkSupplier(person:Person):Boolean {
				if (supplierList.selectedItem == null) {
					return true;
				}
				return person == supplierList.selectedItem;
			}
			
			private function checkDate(date:Date):Boolean {
				if (dateField.selectedRanges.length == 0) {
					return true;
				}
				for each (var obj:Object in dateField.selectedRanges) {
					var start:Date = new Date(obj.rangeStart.time);
					var end:Date = new Date(obj.rangeEnd.time);
					// Move end date to the midnight of the next day.
					end = utils.moveDateByDay(end, 1);
					if (date >= start && date < end) {
						return true;
					}
				}
				return false;
			}
			
			internal function removeSupplyItem(item:SupplyItem):void {
				item.supply.removeSupplyItem(item);
				model.removeSupplyItem(item);
				CairngormUtils.dispatchEvent(JNidEvent.DELETE_SUPPLY_ITEM, item);
			}
			
			private function searchChange(e:Event):void {
                var sort:Sort = new Sort();
                sort.fields = [new SortField("sortIndex"), new SortField("name")];
                people.sort = sort;
                people.filterFunction = 
                    function(obj:Object):Boolean {
                        var person:Person = Person(obj);
                        person.sortIndex = 
                             person.name.indexOf(supplierSearchField.text);
                        return person.sortIndex != -1 && Person(obj).isSupplier;
                    };
                people.refresh();
            }
			
			private function deleteSupplyClick(e:MouseEvent):void {
				var supply:Supply = Supply(supplierList.selectedItem);
				model.removeSupply(supply);
				CairngormUtils.dispatchEvent(JNidEvent.DELETE_SUPPLY, supply);
			}
			
			private function itemListKeyDown(e:KeyboardEvent):void {
				var item:SupplyItem = SupplyItem(supplyItemList.selectedItem);
				if (e.keyCode == Keyboard.DELETE && item != null) {
					removeSupplyItem(item);
				}
			}
			
			private function formatPrice(item:Object,
                                          column:DataGridColumn):String {
                                          	
            	return utils.formatPrice(item[column.dataField]);
            }
		]]>
	</mx:Script>
	
	<mx:DateFormatter id="dateFormatter" formatString="DD/MM/YY J:NN:SS"/>
	
	<mx:Panel title="{resourceManager.getString('SupplyPage', 'Filter')}"
		paddingTop="5"
		width="220" height="100%">
		
		<mx:FormItem label="{resourceManager.getString(
		                          'SupplyPage', 'SearchSupplier')}:"
			paddingRight="5" paddingLeft="5"
			width="100%">
			
			<mx:TextInput id="supplierSearchField"
				change="searchChange(event)"
				width="100%"/>
		</mx:FormItem>
		
		<mx:List id="supplierList"
			labelField="name"
			borderThickness="0"
			width="100%" height="100%"/>
		
		<mx:DateChooser id="dateField"
			cornerRadius="0"
			width="100%"/>
	</mx:Panel>
	
	<mx:Panel title="{resourceManager.getString('SupplyPage', 'Supply')}"
		width="250" height="100%">
		
		<mx:List id="supplyList" 
			width="100%" height="100%"/>
	</mx:Panel>
	
	<mx:Panel title="{resourceManager.getString('SupplyPage', 'Detail')}" 
		layout="vertical"
		paddingLeft="5" paddingRight="5" 
		paddingTop="5"
		width="100%" height="100%">
		
		<mx:HBox 
			width="100%" height="35" 
			paddingTop="5" paddingRight="5" 
			horizontalAlign="right">
			
			<mx:Label text="{supplyList.selectedItem.person.name}"/>
			<mx:Label text="{dateFormatter.format(supplyList.selectedItem.created)}"/>
		</mx:HBox>
		
		<mx:DataGrid id="supplyItemList" 
			dataProvider="{supplyList.selectedItem.supplyItems}"
			textAlign="center"
			keyDown="itemListKeyDown(event)"
			width="100%" height="100%">
			<mx:columns>
				<mx:DataGridColumn
					headerText="{resourceManager.getString('SupplyPage', 'Quantity')}"
					dataField="quantity"
					headerStyleName="dgHeader"
					width="50"/>
				<mx:DataGridColumn
					headerText="{resourceManager.getString('SupplyPage', 'Name')}"
					dataField="name"
					headerStyleName="dgHeader"/>
				<mx:DataGridColumn
					headerText="{resourceManager.getString('SupplyPage', 'Unit')}"
					dataField="unit"
					textAlign="right"
					headerStyleName="dgHeader"
					width="50"/>
				<mx:DataGridColumn
					headerText="{resourceManager.getString('SupplyPage', 'PricePerUnit')}"
					dataField="pricePerUnit"
					labelFunction="formatPrice"
					textAlign="right"
					headerStyleName="dgHeader"
					width="85"/>
				<mx:DataGridColumn
					headerText="{resourceManager.getString('SupplyPage', 'Total')}"
					dataField="total"
					labelFunction="formatPrice" 
					textAlign="right"
					headerStyleName="dgHeader"
					width="85"/>
			</mx:columns>
		</mx:DataGrid>
		
		<mx:HBox paddingRight="5" horizontalAlign="right"
			width="100%" height="35">
			
			<mx:Label text="{supplyList.selectedItem.total}"/>
		</mx:HBox>
		
		<mx:ControlBar horizontalAlign="right">
		
			<mx:Button label="{resourceManager.getString('SupplyPage', 'Delete')}"
				click="deleteSupplyClick(event)"
				enabled="{!supplyList.selectedItem.isCanceled()}"/>
		</mx:ControlBar>
	</mx:Panel>
</mx:HBox>
