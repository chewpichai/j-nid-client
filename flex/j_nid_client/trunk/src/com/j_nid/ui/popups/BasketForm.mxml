<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
    title="ข้อมูลลังมัดจำ" layout="absolute"
	width="400" height="400">
    
    <mx:Script>
        <![CDATA[
			import com.j_nid.utils.Responder;
			import com.j_nid.utils.ServiceUtils;
			import com.j_nid.utils.Utils;
			
			import mx.controls.LinkButton;
			import mx.rpc.events.ResultEvent;
			import mx.utils.StringUtil;
        	
            [Bindable]
            public var basket:XML;
            private var closeBtn:LinkButton;
            private var errMsg:String;
            
            override protected function createChildren():void {
                super.createChildren();
                closeBtn = new LinkButton();
                closeBtn.label = "x";
                closeBtn.addEventListener(MouseEvent.CLICK, closeBtnClick);
                rawChildren.addChild(closeBtn);
            }
            
            override protected function updateDisplayList(unscaledWidth:Number, 
                                            unscaledHeight:Number):void    {
                
                super.updateDisplayList(unscaledWidth, unscaledHeight);    
                var margin:int = 4;    
                var pixelsFromTop:int = 5;
                var pixelsFromRight:int = 10;
                closeBtn.setActualSize(30 + margin, 20 + margin);
                var buttonWidth:int = closeBtn.width;
                var xPos:int = unscaledWidth - buttonWidth - pixelsFromRight;
                var yPos:int = pixelsFromTop;
                closeBtn.move(xPos, yPos);
            }
        
            private function saveProductListener(e:MouseEvent):void {
				var basket:XML = this.basket.copy();
                if (isValid()) {
                    basket.name = nameField.text;
					basket.unit = unitField.value;
					basket.price_per_unit = pricePerUnitField.value;
                    var responder:com.j_nid.utils.Responder =
						new com.j_nid.utils.Responder(resultHandler);
					if (int(basket.id) > 0) {
						ServiceUtils.send("/baskets/" + basket.id + "/",
							"PUT", responder, basket);
					} else {
						ServiceUtils.send("/baskets/",	"POST", responder, basket);
					}
                } else {
                    Utils.showMessage(errMsg, "พบข้อผิดพลาด");
                }
            }
			
			private function resultHandler(data:Object):void {
				close();
			}
            
            private function isValid():Boolean {
                errMsg = "";
                var name:String = StringUtil.trim(nameField.text);
                if (name.length < 1) {
                    errMsg += "กรุณาใส่ชื่อ\n";
                }
                return errMsg.length == 0;
            }
            
            private function closeBtnClick(e:MouseEvent):void {
                close();
            }
            
            private function close():void {
				Utils.hidePopUp(this);
            }
        ]]>
    </mx:Script>
    
    <mx:Form width="100%" height="100%">
        
        <mx:FormItem label="ชื่อ:" required="true" width="100%">
            
            <mx:TextInput id="nameField" text="{basket.name}"
                width="100%" height="30"/>
        </mx:FormItem>
        
        <mx:FormItem label="หน่วย:" width="100%">
            
            <mx:NumericStepper id="unitField"
                value="{basket.unit}"
                minimum="1" maximum="100000"
                width="100%" height="30"/>
        </mx:FormItem>
        
        <mx:FormItem label="ราคาต่อหน่วย:" width="100%">
            
            <mx:NumericStepper id="pricePerUnitField"
                value="{basket.price_per_unit}"
                minimum="0.25" maximum="100000" stepSize="0.25"
                width="100%" height="30"/>
        </mx:FormItem>
        
        <mx:FormItem>
            <mx:Button id="saveBtn"
                label="บันทึก" click="saveProductListener(event)"/>
        </mx:FormItem>
    </mx:Form>
</mx:TitleWindow>
